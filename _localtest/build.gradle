buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath group: 'org.hidetake', name: 'groovy-ssh', version: '2.7.0'
        classpath group: "jaci.gradle", name: "EmbeddedTools", version: "+" 
    }
}

apply plugin: "java"
apply plugin: "cpp"
apply plugin: "c"
apply plugin: "jaci.gradle.EmbeddedTools"

import jaci.gradle.deployers.*
import jaci.gradle.targets.*
import jaci.gradle.toolchains.*

model {
    targets {
        roborio(RemoteTarget) {
            addresses << "172.22.11.2"
            directory "/home/lvuser/deploy"
            user "lvuser"
            password ""
            promptPassword false // Can set true, ignores above
            timeout 3
            failOnMissing true
        }
        other(RemoteTarget)
    }

    deployers {
        test(Deployer) {
            artifacts {
                jar(JavaArtifact) {
                    directory "subdir"
                    filename "filename.jar"
                    order 1
                }
                src(JavaArtifact) {
                    predeploy << "echo SrcPre"
                }

                myfile(FileArtifact) {
                    file "myfile.dat" // same options as above
                }

                my_program(NativeArtifact) {
                    // same options as above
                    libraries true          // deploy shared libraries
                    librootdir "libfiles" // will deploy to include/ and lib/

                    platform "any-32"  // filter deploy units based on platform
                }

                my_other_program(NativeArtifact) {
                    platform "any-32"
                }

                commands(CommandArtifact) {
                    command "whoami"
                }
            }

            predeploy << "pwd"
            postdeploy << "echo Postdeploy"
            targets << "roborio"
            order 1
        }
    }

    platforms {
        'any-32' { architecture "x86" }
        'any-64' { architecture "x86_64" }
        roborio { operatingSystem 'linux'; architecture 'arm' }
    }

    toolChains {
        roborioGcc(CrossGcc) {
            target('roborio') {
                defineTools(it, "arm-frc-linux-gnueabi-", '')
            }
        }
        gcc(Gcc) { }
        visualCpp(VisualCpp) { }
    }

    components {
        my_other_program(NativeLibrarySpec) {
            // targetPlatform "any-32"
            targetPlatform "roborio"
            // targetPlatform "any-64"
            sources.cpp {
                source.srcDirs "src_c_lib"
            }
        }
        my_program(NativeExecutableSpec) {
            targetPlatform "any-32"
            targetPlatform "any-64"
            targetPlatform "roborio"
            sources.cpp {
                source.srcDirs "src_c"
            }
        }
    }
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2'
}
