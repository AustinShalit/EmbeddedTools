buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath group: 'org.hidetake', name: 'groovy-ssh', version: '2.7.0'
        classpath group: "jaci.gradle", name: "EmbeddedTools", version: "+" 
    }
}

apply plugin: "java"
apply plugin: "cpp"
apply plugin: "jaci.gradle.EmbeddedTools"

model {
    targets {
        roborio(RemoteTarget) {
            addresses << "172.22.11.2"
            directory "/home/lvuser/deploy"
            user "lvuser"
            password ""
            promptPassword false // Can set true, ignores above
            timeout 3
            failOnMissing true
        }
        other(RemoteTarget)
    }

    deployers {
        test(Deployer) {
            artifacts {
                jar(JavaArtifact) {
                    directory "subdir"
                    filename "filename.jar"
                    order 1
                }
                src(JavaArtifact) {
                    predeploy << "echo SrcPre"
                }

                myfile(FileArtifact) {
                    file "myfile.dat" // same options as above
                }

                my_program(NativeArtifact) {
                    // same options as above
                    libraries true          // deploy shared libraries
                    librootdir "libfiles" // will deploy to include/ and lib/

                    platform "x86"  // filter deploy units based on platform
                }

                my_other_program(NativeArtifact) {
                    platform "x86"
                }

                commands(CommandArtifact) {
                    command "echo 'Hello World'"
                }
            }

            predeploy << "pwd"
            postdeploy << "echo Postdeploy"
            targets << "roborio"
            order 1
        }
    }

    platforms {
        x86 {
            architecture 'x86'
        }
        x64 {
            architecture 'x86_64'
        }
    }

    components {
        my_other_program(NativeLibrarySpec) {
            targetPlatform "x86"
            targetPlatform "x64"
            sources.cpp {
                source.srcDirs "src_c"
            }
        }
        my_program(NativeExecutableSpec) {
            targetPlatform "x86"
            targetPlatform "x64"
            sources.cpp {
                source.srcDirs "src_c"
                lib project: ":", library: "my_other_program", linkage: "shared"
            }
        }
    }
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}
